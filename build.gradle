plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
}

version = project.mod_version
group = project.maven_group

repositories {

    mavenCentral()

    maven {
        url "https://cursemaven.com"
        content { includeGroup "curse.maven" }
    }
    maven {
        url "https://api.modrinth.com/maven"
        content { includeGroup "maven.modrinth" }
    }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.architectury.dev" }
    maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
    maven { url "https://maven.ladysnake.org/releases" }
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://maven.terraformersmc.com/releases" }
    maven { url "https://maven.tterrag.com/" }
    maven { url "https://mvn.devos.one/snapshots/" }
    maven { url "https://raw.githubusercontent.com/SolidBlock-cn/mvn-repo/main" }
    maven { url "https://ueaj.dev/maven" }

}

dependencies {

    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modImplementation("net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

    modImplementation("pers.solid:brrp-fabric:1.0.1-1.18.2") {
        transitive = false
    }

    //Roughly Enough Items
    modRuntimeOnly("com.terraformersmc:modmenu:3.2.5") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modRuntimeOnly("dev.architectury:architectury-fabric:4.11.93") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modRuntimeOnly("me.shedaniel.cloth:cloth-config-fabric:6.4.90") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("me.shedaniel:RoughlyEnoughItems-fabric:8.3.618") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //Create
    modImplementation("com.simibubi.create:create-fabric-${minecraft_version}:0.5.0.i-944+1.18.2") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("com.tterrag.registrate_fabric:Registrate:MC1.18.2-1.1.7") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //Modern Industrialization
    modRuntimeOnly("com.github.Technici4n:magna:1.7.0-1.18") {
        exclude(group: "io.github.prospector")
        exclude(group: "me.shedaniel.cloth")
        exclude(group: "net.fabricmc.fabric-api")
    }

    modRuntimeOnly("io.github.ladysnake:PlayerAbilityLib:1.5.0") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("maven.modrinth:modern-industrialization:1.2.3") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //Tech Reborn
    modRuntimeOnly("RebornCore:RebornCore-1.18:+") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("TechReborn:TechReborn-1.18:+") {
        exclude(group: "net.fabricmc.fabric-api")
    }

}

loom {

    accessWidenerPath = file("src/main/resources/ht_materials.accesswidener")

    runs {
        client {
            vmArg("-Dmixin.debug.export=true")
        }
    }

}

processResources {

    inputs.property "version", version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "loader_version", loader_version
    inputs.property "fabric_kotlin_version", fabric_kotlin_version
    inputs.property "brrp_version", brrp_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": version,
                "minecraft_version": minecraft_version,
                "loader_version": loader_version,
                "fabric_kotlin_version": fabric_kotlin_version,
                "brrp_version": brrp_version
    }

}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

kotlin {
    jvmToolchain(17)
}